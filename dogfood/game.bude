### BEGIN RAYLIB ###

# Data type definitions
pack Color def
    r -> u8
    g -> u8
    b -> u8
    a -> u8
end

pack Vector2 def
    x -> f32
    y -> f32
end

comp Vector3 def
    xy -> Vector2
    z -> f32
end

comp Rectangle def
    Rec.pos -> Vector2
    Rec.size -> Vector2
end

# Colours
func RAYWHITE -> Color def 245u8 245u8 245u8 255u8 Color end

func RED      -> Color def 230u8  41u8  55u8 255u8 Color end
func YELLOW   -> Color def 253u8 249u8   0u8 255u8 Color end
func BLUE     -> Color def   0u8 121u8 241u8 255u8 Color end
func MAGENTA  -> Color def 255u8   0u8 255u8 255u8 Color end
func GREEN    -> Color def   0u8 228u8  48u8 255u8 Color end
func SKYBLUE  -> Color def 102u8 191u8 255u8 255u8 Color end

# Keys
func KEY_RIGHT -> s32 def 262s32 end
func KEY_LEFT  -> s32 def 263s32 end
func KEY_DOWN  -> s32 def 264s32 end
func KEY_UP    -> s32 def 265s32 end

# Raylib functions
import raylib def
    func s32 s32 ptr InitWindow end
    func CloseWindow end
    func WindowShouldClose -> bool end
    func BeginDrawing end
    func EndDrawing end
    func Color ClearBackground end
    func ptr s32 s32 s32 Color DrawText end
    func ptr s32 MeasureText -> s32 end
    func Vector2 f32 Color DrawCircleV end
    func Vector2 Vector2 Color DrawRectangleV end
    func Vector2 Vector2 Vector2 Color DrawTriangle end
    func Vector2 s32 f32 f32 Color DrawPoly end
    func s32 IsKeyPressed -> bool end
    func s32 IsKeyDown -> bool end
    func Color ColorToHSV -> Vector3 end
    func f32 f32 f32 ColorFromHSV -> Color end
end

### END RAYLIB ###


func string to-cstr -> ptr def
     decomp pop
end

func colours -> array[6 Color] def
    RED
    YELLOW
    BLUE
    MAGENTA
    GREEN
    SKYBLUE
    array[6 Color]
end

comp Player def
    pos -> Vector2
    colour -> int
    speed -> f32
end

comp Triangle def
    v1 -> Vector2
    v2 -> Vector2
    v3 -> Vector2
end

func Player get-body -> Rectangle def
    # pos is in centre of body
    pos
    var player-width -> f32
        player-height -> f32
    end
    40f32 <- player-width
    80f32 <- player-height
    x player-width 0.5f32 * - <- x
    y player-height 0.5f32 * - <- y
    player-width player-height Vector2
    Rectangle
    swap pop
end

func Player get-nose-cone -> Triangle def
    get-body decomp over  # top corner of body
    # corner1 size corner2
    x rot x rot +
    rot swap <- x
    # corner1 corner2 corner2 size (c.x-s.x/2)
    swap over
    x rot x 0.5f32 * rot swap -
    # corner1 corner2 (c.x-s.x/2) corner2 (c.y+s.x)
    swap unpack pop
    rot y rot - <- y
    swap <- x
    Triangle
end

# func Player get-left-fin -> Triangle def
# end

# func Player get-right-fin -> Triangle def
# end

func int int int clamp-between -> int def
    over -
    swap rot over -
    rot % +
end

func int next-colour -> int def
    1 + 0 6 clamp-between
end

func int get-colour -> Color def
    colours [swap]
    swap pop
end

func Player draw-player def
    var player-colour -> Color end
    colour get-colour <- player-colour
    dupe get-body decomp player-colour DrawRectangleV
    get-nose-cone decomp
    player-colour DrawTriangle
end

func Player move-right -> Player def
    var player-speed -> f32 end
    speed <- player-speed
    pos x player-speed + <- x <- pos
end

func Player move-left -> Player def
    var player-speed -> f32 end
    speed <- player-speed
    pos x player-speed - <- x <- pos
end

func Player move-down -> Player def
    var player-speed -> f32 end
    speed <- player-speed
    pos y player-speed + <- y <- pos
end

func Player move-up -> Player def
    var player-speed -> f32 end
    speed <- player-speed
    pos y player-speed - <- y <- pos
end


func screen-width  -> s32 def 1200s32 end
func screen-height -> s32 def 800s32 end
func screen-title  -> string def "Raylib Game in Bude" end

func screen-centre -> Vector2 def
    screen-width 0.5f32 *
    screen-height 0.5f32 *
    Vector2
end


screen-width screen-height screen-title to-cstr InitWindow

var player -> Player end
screen-centre 0 .2f32 Player <- player

func should-go-right -> bool def
    'D' to s32 IsKeyDown
    KEY_RIGHT IsKeyDown
    or
end

func should-go-left -> bool def
    'A' to s32 IsKeyDown
    KEY_LEFT IsKeyDown
    or
end

func should-go-down -> bool def
    'S' to s32 IsKeyDown
    KEY_DOWN IsKeyDown
    or
end

func should-go-up -> bool def
    'W' to s32 IsKeyDown
    KEY_UP IsKeyDown
    or
end

while WindowShouldClose not do
    BeginDrawing
    RAYWHITE ClearBackground

    var text -> ptr
        font-size -> s32
    end
    "Hello, Raylib!" to-cstr <- text
    80s32 <- font-size

    player draw-player

    if should-go-right then
        player move-right <- player
    end
    if should-go-left then
        player move-left <- player
    end
    if should-go-up then
        player move-up <- player
    end
    if should-go-down then
        player move-down <- player
    end
    if ' ' to s32 IsKeyPressed then
        player colour next-colour <- colour <- player
    end


    EndDrawing
end

CloseWindow
